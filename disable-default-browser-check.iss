; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Disable Default Browser Check"
#define MyAppVersion "1.0"
#define MyAppPublisher "Informationsea"
#define MyAppURL "https://informationsea.info/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{7F80CA3A-4C29-4B34-AC8A-3A2FBB6D3A6B}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
CreateAppDir=yes
DefaultDirName={autopf}\DisableDefaultBrowserCheck
DisableDirPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
OutputBaseFilename=disable-default-browser-check
Compression=lzma
SolidCompression=yes
WizardStyle=modern
PrivilegesRequiredOverridesAllowed=commandline 
PrivilegesRequired=lowest
SetupIconFile=icon.ico
UninstallDisplayIcon={app}\icon.ico
LicenseFile=LICENSE

[Types]                                     
Name: "recommended"; Description: "Recommended Configuration"
Name: "full"; Description: "Full Configuration"
Name: "custom"; Description: "Custom Configuration"; Flags: iscustom

[Components]
Name: "default_browser_check"; Description: "Disable Default Browser Check"; Types: full recommended
Name: "default_browser_check\edge"; Description: "Disable Default Browser Check (Microsoft Edge)"; Types: full recommended
Name: "default_browser_check\chrome"; Description: "Disable Default Browser Check (Google Chrome)"; Types: full recommended    
Name: "default_browser_check\firefox"; Description: "Disable Default Browser Check (Firefox)"; Types: full recommended
Name: "password_manager"; Description: "Disable Password Manager"; Types: full
Name: "password_manager\edge"; Description: "Disable Password Manager (Microsoft Edge)"; Types: full        
Name: "password_manager\chrome"; Description: "Disable Password Manager (Google Chrome)"; Types: full
Name: "password_manager\firefox"; Description: "Disable Password Manager (Firefox)"; Types: full
Name: "news_feed"; Description: "Disable News Feed on New Tab (Microsoft Edge)"; Types: full recommended
Name: "quick_links"; Description: "Disable Quick Links on New Tab"; Types: full
Name: "quick_links\edge"; Description: "Disable Quick Links on New Tab (Microsoft Edge)"; Types: full
Name: "top_sites"; Description: "Disable Top Sites on New Tab"; Types: full
Name: "top_sites\edge"; Description: "Disable Top Sites on New Tab (Microsoft Edge)"; Types: full   
Name: "top_sites\firefox"; Description: "Disable Top Sites on New Tab (Firefox)"; Types: full
Name: "top_sites\firefox_sponsored"; Description: "Disable Sponsored Top Sites on New Tab (Firefox)"; Types: full recommended
Name: "tab_sleep"; Description: "Disable Sleeping Tabs (Microsoft Edge)"; Types: full
Name: "custom_background"; Description: "Disable Background Image on New Tab (Microsoft Edge)"; Types: full   
Name: "collections"; Description: "Disable Collections (Microsoft Edge)"; Types: full recommended
Name: "drop"; Description: "Disable Drop Service (Microsoft Edge)"; Types: full recommended
Name: "follow"; Description: "Disable Follow Service (Microsoft Edge)"; Types: full recommended
Name: "shopping"; Description: "Disable Shopping Assistant (Microsoft Edge)"; Types: full recommended   
Name: "wallet_checkout"; Description: "Disable Wallet Checkout (Microsoft Edge)"; Types: full recommended   
Name: "wallet_etree"; Description: "Disable Wallet E-Tree (Microsoft Edge)"; Types: full recommended
Name: "browser_import"; Description: "Disable Browser Import"; Types: full recommended               
Name: "browser_import\edge"; Description: "Disable Browser Import (Microsoft Edge)"; Types: full recommended  
Name: "browser_import\firefox"; Description: "Disable Browser Import (Firefox)"; Types: full                   
Name: "browser_import\chrome"; Description: "Disable Browser Import (Google Chrome)"; Types: full        
Name: "mini_menu"; Description: "Disable Mini Menu (Microsoft Edge)"; Types: full recommended                 
Name: "first_run"; Description: "Disable First Run Experience (Microsoft Edge)"; Types: full recommended      
Name: "set_desktop_background"; Description: "Disable Set As Desktop Background... Menu (Firefox)"; Types: full recommended   
Name: "translate"; Description: "Disable Translate"; Types: full                 
Name: "translate\edge"; Description: "Disable Translate (Microsoft Edge)"; Types: full   
Name: "translate\chrome"; Description: "Disable Translate (Google Chrome)"; Types: full            


[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "icon.ico"; DestDir: "{app}"

[Registry]
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; Flags: uninsdeletekeyifempty
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "DefaultBrowserSettingEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "default_browser_check\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "DefaultBrowserSettingsCampaignEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "default_browser_check\edge"
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "PasswordManagerEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "password_manager\edge"
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "NewTabPageContentEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "news_feed"
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "NewTabPageQuickLinksEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "quick_links\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "NewTabPageHideDefaultTopSites"; ValueData: 1; Flags: uninsdeletevalue; Components: "top_sites\edge"
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "SleepingTabsEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "tab_sleep"
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "NewTabPageAllowedBackgroundTypes"; ValueData: 3; Flags: uninsdeletevalue; Components: "custom_background"
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "EdgeCollectionsEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "collections"
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "EdgeEDropEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "drop"
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "EdgeFollowEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "follow"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "EdgeShoppingAssistantEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "shopping"
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "EdgeWalletCheckoutEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "wallet_checkout"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "EdgeWalletEtreeEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "wallet_etree"        
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "AutoImportAtFirstRun"; ValueData: 4; Flags: uninsdeletevalue; Components: "browser_import\edge"   
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "ImportAutofillFormData"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "ImportBrowserSettings"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\edge"   
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "ImportCookies"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "ImportExtensions"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "ImportFavorites"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "ImportHistory"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "ImportHomepage"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "ImportOnEachLaunch"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "ImportOpenTabs"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "ImportPaymentInfo"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "ImportSavedPasswords"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "ImportSearchEngine"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "ImportShortcuts"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "ImportStartupPageSettings"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\edge"  
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "QuickSearchShowMiniMenu"; ValueData: 0; Flags: uninsdeletevalue; Components: "mini_menu"             
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "HideFirstRunExperience"; ValueData: 0; Flags: uninsdeletevalue; Components: "first_run"          
Root: HKA; Subkey: "SOFTWARE\Policies\Microsoft\Edge"; ValueType: dword; ValueName: "TranslateEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "translate\edge"

Root: HKA; Subkey: "SOFTWARE\Policies\Mozilla\Firefox"; Flags: uninsdeletekeyifempty
Root: HKA; Subkey: "SOFTWARE\Policies\Mozilla\Firefox"; ValueType: dword; ValueName: "DontCheckDefaultBrowser"; ValueData: 1; Flags: uninsdeletevalue; Components: "default_browser_check\firefox"
Root: HKA; Subkey: "SOFTWARE\Policies\Mozilla\Firefox"; ValueType: dword; ValueName: "DisableProfileImport"; ValueData: 1; Flags: uninsdeletevalue; Components: "browser_import\firefox"        
Root: HKA; Subkey: "SOFTWARE\Policies\Mozilla\Firefox"; ValueType: dword; ValueName: "DisableSetDesktopBackground"; ValueData: 1; Flags: uninsdeletevalue; Components: "set_desktop_background"
Root: HKA; Subkey: "SOFTWARE\Policies\Mozilla\Firefox"; ValueType: dword; ValueName: "PasswordManagerEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "password_manager\firefox"    
Root: HKA; Subkey: "SOFTWARE\Policies\Mozilla\Firefox\FirefoxHome"; Flags: uninsdeletekeyifempty
Root: HKA; Subkey: "SOFTWARE\Policies\Mozilla\Firefox\FirefoxHome"; ValueType: dword; ValueName: "TopSites"; ValueData: 0; Flags: uninsdeletevalue; Components: "top_sites\firefox"   
Root: HKA; Subkey: "SOFTWARE\Policies\Mozilla\Firefox\FirefoxHome"; ValueType: dword; ValueName: "SponsoredTopSites"; ValueData: 0; Flags: uninsdeletevalue; Components: "top_sites\firefox_sponsored" 

Root: HKA; Subkey: "SOFTWARE\Policies\Google\Chrome"; Flags: uninsdeletekeyifempty                                                                                                     
Root: HKA; Subkey: "SOFTWARE\Policies\Google\Chrome"; ValueType: dword; ValueName: "DefaultBrowserSettingEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "default_browser_check\chrome"
Root: HKA; Subkey: "SOFTWARE\Policies\Google\Chrome"; ValueType: dword; ValueName: "PasswordManagerEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "password_manager\chrome"   
Root: HKA; Subkey: "SOFTWARE\Policies\Google\Chrome\Recommended"; Flags: uninsdeletekeyifempty
Root: HKA; Subkey: "SOFTWARE\Policies\Google\Chrome\Recommended"; ValueType: dword; ValueName: "ImportAutofillFormData"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\chrome"  
Root: HKA; Subkey: "SOFTWARE\Policies\Google\Chrome\Recommended"; ValueType: dword; ValueName: "ImportBookmarks"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\chrome"
Root: HKA; Subkey: "SOFTWARE\Policies\Google\Chrome\Recommended"; ValueType: dword; ValueName: "ImportHistory"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\chrome"
Root: HKA; Subkey: "SOFTWARE\Policies\Google\Chrome\Recommended"; ValueType: dword; ValueName: "ImportSavedPasswords"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\chrome"     
Root: HKA; Subkey: "SOFTWARE\Policies\Google\Chrome\Recommended"; ValueType: dword; ValueName: "ImportSearchEngine"; ValueData: 0; Flags: uninsdeletevalue; Components: "browser_import\chrome"         
Root: HKA; Subkey: "SOFTWARE\Policies\Google\Chrome"; ValueType: dword; ValueName: "TranslateEnabled"; ValueData: 0; Flags: uninsdeletevalue; Components: "translate\chrome"


[Code]
function InitializeSetup: Boolean;
begin      
  Result := True;
  if RegKeyExists(HKEY_AUTO, 'Software\Microsoft\Windows\CurrentVersion\Uninstall\{7F80CA3A-4C29-4B34-AC8A-3A2FBB6D3A6B}_is1') then
  begin
    MsgBox('Please uninstall Disable Default Browser Check before re-cofiguration', mbError, MB_OK); 
    Result := False;
    exit;
  end;
  if RegKeyExists(HKEY_CURRENT_USER, 'SOFTWARE\Policies\Microsoft\Edge') then
  begin
    MsgBox('Some Microsoft Edge policy is found. Please remove existing policy to configure.', mbError, MB_OK);
    Result := False;
    exit;
  end;
  if RegKeyExists(HKEY_CURRENT_USER, 'SOFTWARE\Policies\Mozilla\Firefox') then
  begin
    MsgBox('Some Mozilla Firefox policy is found. Please remove existing policy to configure.', mbError, MB_OK);
    Result := False;
    exit;
  end;
  if RegKeyExists(HKEY_CURRENT_USER, 'SOFTWARE\Policies\Google\Chrome') then
  begin
    MsgBox('Some Google Chrome policy is found. Please remove existing policy to configure.', mbError, MB_OK);
    Result := False;
    exit;
  end;    
  if RegKeyExists(HKEY_LOCAL_MACHINE, 'SOFTWARE\Policies\Microsoft\Edge') then
  begin
    MsgBox('Some Microsoft Edge policy is found. Please remove existing policy to configure.', mbError, MB_OK);
    Result := False;
    exit;
  end;
  if RegKeyExists(HKEY_LOCAL_MACHINE, 'SOFTWARE\Policies\Mozilla\Firefox') then
  begin
    MsgBox('Some Mozilla Firefox policy is found. Please remove existing policy to configure.', mbError, MB_OK);
    Result := False;
    exit;
  end;
  if RegKeyExists(HKEY_LOCAL_MACHINE, 'SOFTWARE\Policies\Google\Chrome') then
  begin
    MsgBox('Some Google Chrome policy is found. Please remove existing policy to configure.', mbError, MB_OK);
    Result := False;
    exit;
  end;
end;
 
